{ types, ... }: {
  inputs = {
    mkWrapper.from = { parent }: parent.mkWrapper;
    nixpkgs.from = { parent }: parent.nixpkgs;
  };

  options = {
    configFile = {
      type = types.pathLike;
      description = ''
        `config.kdl` file to be injected into the wrapped package.

        See the niri documentation for syntax and valid options:
        https://github.com/niri-wm/niri/wiki/Configuration:-Introduction
      '';
    };

    package = {
      type = types.derivation;
      description = "The niri package to be wrapped.";
      defaultFunc = { inputs }: inputs.nixpkgs.pkgs.niri;
    };
  };

  impl =
    { options, inputs }:
    if options ? configFile then
      inputs.mkWrapper {
        inherit (options) package;
        # Hack modified and gotten from https://github.com/Lassulus/wrappers/blob/main/modules/niri/module.nix
        postWrap = ''
          cp $out/share/systemd/user/niri.service niri.service
          chmod +w niri.service
          cat >> niri.service<<EOF
          [Service]
          ExecStart=
          ExecStart=$out/bin/niri --session
          ExecReload=$out/bin/niri msg action load-config-file --path ${options.configFile}
          X-ReloadIfChanged=true
          [Unit]
          X-Reload-Triggers=${options.configFile}
          EOF
          cp --remove-destination niri.service $out/share/systemd/user/niri.service
        '';
        environment = {
          NIRI_CONFIG = options.configFile;
        };
      }
    else
      options.package;

  meta = {
    maintainers = [ "coca" ];
  };
}
